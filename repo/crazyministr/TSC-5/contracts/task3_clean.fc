forall X -> int is_null?(X x) asm "ISNULL";


;; int get_current_version() {
;;     return get_data().begin_parse().preload_uint(32);
;; }

cell get_storage() {
    return get_data().begin_parse().preload_ref();
}

cell wrap_storage(int version_id, cell storage) {
    return begin_cell()
        .store_uint(version_id, 32)
        .store_ref(storage)
    .end_cell();
}

int version() method_id {
    return get_data().begin_parse().preload_uint(32);
}


;; <<<<< Custom version-specific code begins
;; This section (everything between << and >> characters) will be fully substituted for each version.
;; This is an IMPORTANT part, and these exact lines with <<<<< and >>>>> must be present in your code for the testing system to work correctly.
;; All the code provided here serves as an example of the version-code, which your update code must be compatible with.
;; Refer to the "3-example" directory for more version examples.

;; from counter-v0.fc
cell process_message(cell storage, int msg_value, int balance, cell in_msg_full, slice in_msg_body) impure {
    slice cs = storage.begin_parse();
    int current_amount = cs.preload_uint(32);
    return begin_cell().store_uint(current_amount + 1, 32).end_cell();
}

cell migrate_one(cell old_storage) { ;; it's just a placeholder that is required for correct compilation
    return old_storage;
}

;; Custom version-specific code ends >>>>>

() recv_internal(int msg_value, int balance, cell in_msg_full, slice in_msg_body) impure {
    ;; if (in_msg_body.slice_empty?()) {
    ;;     return ();
    ;; }

    int expected_version = in_msg_body~load_uint(32);  ;; expected version
    ifnot (expected_version) {
        cell default_storage = get_data();
        set_data(wrap_storage(1, default_storage));
        return ();
    }

    cell new_code = in_msg_body~load_maybe_ref(); ;; expected code
    cell migrations = in_msg_body~load_dict(); ;; migrations

    int current_version = get_data().begin_parse().preload_uint(32);
    ;; throw_unless(200, expected_version >= current_version);

    cell storage = get_storage();
    ifnot (migrations.dict_empty?()) {
        int key = current_version;
        do {
            (slice cs, int f) = migrations.udict_get?(32, key);
            throw_unless(400, f);
            int new_version = cs~load_uint(32);
            cell migration_code = cs.preload_maybe_ref();
            ifnot (is_null?(migration_code)) {
                set_c3(migration_code.begin_parse().bless());
                storage = migrate_one(storage);
            }
            key = new_version;
            ;; throw_if(400, key > expected_version);
            if (key == expected_version) { f = 0; }
        } until (~ f);
    }
    ;; elseif (expected_version > current_version) {  ;; need update
    ;;     throw(200);
    ;; }

    if (expected_version > current_version) {  ;; need update
        throw_if(200, is_null?(new_code));
        ;; accept_message();
        set_code(new_code);
        set_c3(new_code.begin_parse().bless());
    }

    cell payload = in_msg_body.preload_ref();
    storage = process_message(storage, msg_value, balance, in_msg_full, payload.begin_parse());
    set_data(wrap_storage(expected_version, storage));
}

