#include "pton/stdlib.fc";
#include "imports/jetton-utils.fc";
#include "imports/params.fc";
#include "pton/error-codes.fc";
#include "pton/op-codes.fc";
#include "pton/messages.fc";

const int fee::gas_consumption = 13000000;
const special::unwrap_op_value = 8388449;
const special::unwrap_op_length = 23;

(slice, slice, cell) load_data() inline {
  slice ds = get_data().begin_parse();
  return (ds~load_msg_addr(), ds~load_msg_addr(), ds~load_ref());
}


(int, slice, slice, cell) get_wallet_data() method_id {
  (slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
  return (pair_first(get_balance()), owner_address, jetton_master_address, jetton_wallet_code);
}

() burn_tokens (slice sender_address, slice to_wallet_address, int jetton_amount, int fwd_fee) impure inline {
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = get_wallet_data();

  throw_unless(error::unauthorized_transfer, equal_slice_bits(owner_address, sender_address));

  balance -= fee::gas_consumption + fwd_fee + jetton_amount;
  throw_unless(error::not_enough_tons, balance > 0);

  var msg = begin_cell()
    .store_msg_flag(msg_flag::non_bounceable)
    .store_slice(to_wallet_address)
    .store_coins(jetton_amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1);

  send_raw_message(msg.end_cell(), PAY_FEES_SEPARATELY);
}

() send_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure inline {
  int query_id = in_msg_body~load_query_id();
  int jetton_amount = in_msg_body~load_coins();
  slice to_owner_address = in_msg_body~load_msg_addr();
  force_chain(to_owner_address);
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = get_wallet_data();

  throw_unless(error::unauthorized_transfer, equal_slice_bits(owner_address, sender_address));

  cell state_init = calculate_proxy_wallet_state_init(to_owner_address, jetton_master_address, jetton_wallet_code);
  slice to_wallet_address = calculate_jetton_wallet_address(state_init);
  slice response_address = in_msg_body~load_msg_addr();
  cell custom_payload = in_msg_body~load_dict();
  int forward_ton_amount = in_msg_body~load_coins();
  throw_unless(708, slice_bits(in_msg_body) >= 1);
  slice either_forward_payload = in_msg_body;
  int fwd_count = forward_ton_amount ? 2 : 1;

  int amount = max(jetton_amount + forward_ton_amount + fwd_count * fwd_fee + 2 * fee::gas_consumption, msg_value);

  if(equal_slice_bits(owner_address, to_owner_address)) {
    var msg = begin_cell()
    .store_msg_flag(msg_flag::bounceable)
    .store_slice(to_wallet_address)
    .store_coins(amount)
    .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
    .store_ref(state_init);
    var msg_body = begin_cell()
      .store_op(op::internal_transfer)
      .store_query_id(query_id)
      .store_coins(jetton_amount)
      .store_slice(owner_address)
      .store_slice(response_address)
      .store_coins(forward_ton_amount)
      .store_slice(either_forward_payload)
      .end_cell();

    msg = msg.store_ref(msg_body);
    send_raw_message(msg.end_cell(), PAY_FEES_SEPARATELY);
  } else {
    burn_tokens(sender_address, to_owner_address, jetton_amount, fwd_fee);
  }
}

() receive_tokens_basics(int fwd_fee, int msg_value, slice owner_address, int query_id, int jetton_amount, slice from_address, slice in_msg_body) impure inline {

  int forward_ton_amount = in_msg_body~load_coins();

  msg_value -= fee::gas_consumption + jetton_amount;

  throw_unless(error::not_enough_tons, msg_value > 0);

  if(forward_ton_amount) {
    msg_value -= forward_ton_amount + fwd_fee;

    slice either_forward_payload = in_msg_body;
    var msg = null();

    slice forward_payload = either_forward_payload;
    if(forward_payload~load_uint(1)) {
      forward_payload = forward_payload~load_ref().begin_parse();
    }
    int need_transfer_notification = true;
    if(forward_payload.slice_bits() == special::unwrap_op_length) {
      if(forward_payload.preload_uint(special::unwrap_op_length) == special::unwrap_op_value) {
        need_transfer_notification = false;

        var msg_body = begin_cell()
            .store_op(op::unwrap_notification)
            .store_query_id(query_id)
            .store_coins(jetton_amount + forward_ton_amount)
            .store_slice(from_address)
            .end_cell();
        msg = begin_cell()
              .store_msg_flag(msg_flag::non_bounceable)
              .store_slice(owner_address)
              .store_coins(jetton_amount + forward_ton_amount)
              .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
              .store_ref(msg_body);
      }
    }
    if(need_transfer_notification) {
      var msg_body = begin_cell()
          .store_op(op::transfer_notification)
          .store_query_id(query_id)
          .store_coins(jetton_amount)
          .store_slice(from_address)
          .store_slice(either_forward_payload)
          .end_cell();

      msg = begin_cell()
        .store_msg_flag(msg_flag::non_bounceable)
        .store_slice(owner_address)
        .store_coins(forward_ton_amount)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body);
    }
    send_raw_message(msg.end_cell(), PAY_FEES_SEPARATELY);
  }

  if ((owner_address.preload_uint(2) != 0) & (msg_value > 0)) {
    var msg = begin_cell()
      .store_msg_flag(msg_flag::non_bounceable)
      .store_slice(owner_address)
      .store_coins(msg_value)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_op(op::excesses)
      .store_query_id(query_id);
    send_raw_message(msg.end_cell(), IGNORE_ERRORS);
  }
}

() receive_tokens (slice in_msg_body, slice sender_address, int fwd_fee, int msg_value) impure inline {
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = get_wallet_data();
  int query_id = in_msg_body~load_query_id();
  int jetton_amount = in_msg_body~load_coins();
  slice owner = in_msg_body~load_msg_addr();
  slice from_address = in_msg_body~load_msg_addr();

  throw_unless(error::unauthorized_incoming_transfer,
      equal_slice_bits(calculate_user_jetton_proxy_wallet_address(owner, jetton_master_address, jetton_wallet_code), sender_address)
  );

  return receive_tokens_basics(fwd_fee, msg_value, owner_address, query_id, jetton_amount, from_address, in_msg_body);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
  if (in_msg_body.slice_empty?()) {
    return ();
  }

  slice cs = in_msg_full.begin_parse();
  int flags = cs~load_uint(4);
  if (flags & 1) {
    return ();
  }
  slice sender_address = cs~load_msg_addr();
  cs~load_msg_addr();
  cs~load_coins();
  cs~skip_bits(1);
  cs~load_coins();
  int fwd_fee = muldiv(cs~load_coins(), 3, 2);

  int op = in_msg_body~load_op();

  if (op == op::transfer) {
    send_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
    return ();
  }

  if (op == op::internal_transfer) {
    receive_tokens(in_msg_body, sender_address, fwd_fee, msg_value);
    return ();
  }

  throw(error::unknown_op);
}