() handle::return_lp_transaction(slice in_msg_body) impure inline_ref {
    in_msg_body~skip_bits(64);
    slice sender_addr = in_msg_body~load_msg_addr();
    int lp_amount = in_msg_body~load_coins();
    var msg_body = begin_cell()
        .store_uint(op::transfer(), 32)
        .store_uint(cur_lt(), 64)
        .store_coins(lp_amount)
        .store_slice(sender_addr)
        .store_slice(my_address())
        .store_uint(0, 1)
        .store_coins(0)
        .store_uint(0, 1);
    
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(lp_jwall_addr)
        .store_coins(0)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body.end_cell());

    send_raw_message(msg.end_cell(), 64);
}


() handle::return_ton_transaction(slice sender_addr) impure inline_ref {
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(sender_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(0, 32)
        .store_slice(msg::freeze_msg());
    
    send_raw_message(msg.end_cell(), 2 + 64);
}


() handle::return_biton_transaction(slice sender_addr, int msg_value, int jetton_amount) impure inline_ref {
    var msg_body = begin_cell()
        .store_uint(op::transfer(), 32)
        .store_uint(cur_lt(), 64) ;; query id
        .store_coins(jetton_amount)
        .store_slice(sender_addr) ;; destination
        .store_slice(my_address()) ;; response destination
        .store_uint(0, 1) ;; payload
        .store_coins(msg_value - operation_price) ;; fwd ton amount
        .store_uint(0, 1);
    
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(jetton_addr)
        .store_coins(0)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body.end_cell());
    
    send_raw_message(msg.end_cell(), 64);
}



(
    int,    ;; op
    slice,  ;; sender addr
    int,    ;; jetton amount
    int,    ;; max price
    slice   ;; err addr (should sent coins if we have error)
) handle::parse_transfer_body(slice in_msg_body) {
    in_msg_body~skip_bits(64); ;; query id
    int jetton_amount = in_msg_body~load_coins();
    slice sender_addr = in_msg_body~load_msg_addr();
    slice routing_addr = sender_addr;

    int op = 0;
    int max_price = 0;

    slice fwd_payload = in_msg_body~load_int(1) ;; if we have 1 -> load_ref
        ? in_msg_body~load_ref().begin_parse()
        : in_msg_body;


    ;; if we have message with empty body then we set op = op_buy, and calculate max price
    if (fwd_payload.slice_data_empty?()) {
        op = op::buy();
        max_price = plus_percent(get_linear_price(), max_price_percentage, 1000000000);
    }
    else {
        op = fwd_payload~load_uint(32);
        if (op == op::buy()) { max_price = fwd_payload~load_grams(); } ;; if we have op = op buy we can load max price
        if (op == op::routing()) { 
            fwd_payload~skip_bits(64);
            routing_addr = fwd_payload~load_msg_addr();
            } ;; TODO routing
    }

    return (op, sender_addr, jetton_amount, max_price, routing_addr);
}



;; deploy lp wallets and remove jetton code 
() handle::deploy_lp_wallets() impure {
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(calculate_minter_address())
        .store_coins(operation_price)
        .store_uint(6, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)  ;; ...110
        .store_ref(calculate_minter_state_init());
    
    send_raw_message(msg.end_cell(), 1);


    lp_minter_addr = calculate_minter_address();
    lp_jwall_addr = calculate_jetton_wallet_address();


    jetton_minter_code = begin_cell().end_cell();
    jetton_wallet_code = begin_cell().end_cell();
    pack_data();

    return ();
}



;; receive jettons and change jetton addr
() handle::init_jetton(slice in_msg_body, slice sender_address) impure {
    in_msg_body~skip_bits(32 + 64);
    biton_balance = in_msg_body~load_coins();
    jetton_addr = sender_address;
    pack_data();
    return();
}


() handle::user_methods(slice sender_address, int msg_value, slice in_msg_body) impure {
    ;; if we have empty message 
    if (in_msg_body.slice_empty?()) { ;; just buy biton
        if (freeze? == true) {   ;; return transaction if we have freeze status
            handle::return_ton_transaction(sender_address);
            return ();
        }
        usr::buy_biton(msg_value, sender_address, in_msg_body);
        return ();
    }

    int op = in_msg_body~load_uint(32);
    if (op == op::buy()) {
        if (freeze? == true) {   ;; return transaction if we have freeze status
            handle::return_ton_transaction(sender_address);
            return ();
        }
        usr::buy_biton(msg_value, sender_address, in_msg_body);
        return ();
    }
    ;; ignore excesses and save it to service collect fee
    if (op == op::excesses()) {
        service_collect_fee += msg_value;
        pack_data();
        return ();
    }

    ;; if we have transfer notification 
    if (op == op::transfer_notification()) {
        ;; throw if sender != jetton address
        throw_unless(err::fake_addr(), equal_slices(jetton_addr, sender_address));
        ;; parse message from jetton wallet
        (int op, slice sender_addr, int jetton_amount, int max_price, slice routing_addr) = handle::parse_transfer_body(in_msg_body);
        
        if (freeze? == true) {   ;; return transaction if we have freeze status
            handle::return_biton_transaction(sender_addr, msg_value, jetton_amount);
            return ();
        }

        
        ;; if jettons for buy ton
        if (op == op::buy()) {
            service_collect_fee += msg_value;
            usr::buy_ton(msg_value, sender_addr, jetton_amount, max_price, routing_addr);
        }
        ;; if user want add liquidity
        if (op == op::add_liquidity()) {
            lp::add_liquidity(msg_value, sender_addr, jetton_amount);
        }
        return ();
    }
    ;; throw if no methods
    throw(0xffff);
}


;; methods from admin address
() handle::admin_methods(slice in_msg_body) impure {
    throw_if(err::no_op(), in_msg_body.slice_empty?()); 
    int op = in_msg_body~load_uint(32);
    ;; if we have comment
    if (op == 0) {
        if (equal_slices(response::withdraw_fee(), in_msg_body)) {
            adm::withdraw_fee();
        }
        return ();
    }
    ;; method for change fee
    if (op == op::change_service_fee()) {
        adm::change_service_fee(in_msg_body);
        return ();
    }

    ;; change operation price
    if(op == op::change_operation_price()) {
        adm::change_operation_price(in_msg_body);
        return ();
    }

    if (op == op::freeze_contract()) {
        freeze? = true;
        pack_data();
        return ();
    }

    if (op == op::unfreeze_contract()) {
        freeze? = false;
        pack_data();
        return ();
    }

    ;;
    ;; allow to set new code and data by smc owner
    ;;
    ;; _ set_data_code#03f2 new_code:(Maybe ^Cell) 
    ;;      new_data:(Maybe ^Cell) = in_msg_body;
    ;;
    if (op == op::set_data_code()) {
        if (in_msg_body~load_int(1)) { set_code(in_msg_body~load_ref()); }
        if (in_msg_body~load_int(1)) { set_data(in_msg_body~load_ref()); }
    
        return ();
    }

    ;; no methods
    throw(0xffff);
}
