#!/usr/bin/fift -s

// Copyright (c) 2019 adoriasoft
// Andrey Lekar andrey_lekar@adoriasoft.com
// Alexey Pozhylenkov alex_pozhilenkov@adoriasoft.com
// Volodymyr Kostanda ceo@adoriasoft.com

"TonUtil.fif" include

{ ."usage: " @' $0 type ." <query-id> <owner> <recipient> <amount>" cr
  ."Creates a request to transfer tokens approved by the owner to be spent by the "
  ."spender (sender of this message) using the token contract." cr
  ."<query-id> defines query id for the message, may remain 0 or 1 for messages from simple wallets." cr
  ."<owner> is a TON address or a file if prefixed with @. <owner> should approve his tokens "
  ."to be spend by the spender." cr
  ."<recipient> is a TON address or a file if prefixed with @. <recipient> will receive the tokens." cr
  ."<amount> amount of tokens to be sent." cr
  ."Transfer query body is saved into queries/body-transfer-from.boc. "
  ."Query body should not be directly sent to the network. Use wallet.fif script with -B <query-body> parameter "
  ."to embed query body in the wallet contract query." cr
  ."Example: " cr
  ."fift -s transfer-from.fif 0 @wallets/alice-wallet.addr @wallets/bob-wallet.addr 10" cr
  1 halt
} : usage

$# 4 < ' usage if

$1 parse-int =: query_id
$2 =: owner_or_file
$3 =: recipient_or_file
$4 parse-int =: amount
"queries/body-transfer-from.boc" constant deployname

owner_or_file true parse-load-address
drop
constant owner_address
constant owner_network

recipient_or_file true parse-load-address
drop
constant recipient_address
constant recipient_network

3 constant op // op = transferFrom

<b op 32 u, query_id 64 u, amount 256 u, owner_network 8 i, owner_address 256 u, recipient_network 8 i, recipient_address 256 u, b> constant intmsg

intmsg ."Internal message for the contract is " <s csr. cr
intmsg 2 boc+>B dup Bx. cr
deployname tuck B>file
."(Saved wallet creating query to file " type .")" cr
