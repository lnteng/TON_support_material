#include "imports/stdlib.fc";
#include "jetton/jetton-utils.fc";
#include "constants.fc";

(slice, int) begin_parse_exotic (cell c) asm "XCTOS";

global slice data::jetton_minter;
global cell data::jetton_wallet_code;
global slice data::jetton_wallet;
global int data::merkle_root;
global cell data::helper_code;

global int context::op;
global slice context::sender;
global int context::query_id;

() load_data () impure inline {
    slice ds = get_data().begin_parse();
    data::jetton_minter = ds~load_msg_addr();
    data::jetton_wallet_code = ds~load_ref();
    data::jetton_wallet = calculate_user_jetton_wallet_address(my_address(), data::jetton_minter, data::jetton_wallet_code);
    data::merkle_root = ds~load_uint(256);
    data::helper_code = ds~load_ref();
}

(cell) helper_stateinit (slice entry) {
    return begin_cell()
        .store_uint(6, 5)
        .store_ref(data::helper_code)
        .store_ref(begin_cell()
            .store_uint(0, 1)
            .store_slice(my_address())
            .store_slice(entry)
        .end_cell())
    .end_cell();
}

(slice) helper_address (cell stateinit) {
    return begin_cell()
        .store_uint(0x400, 11)
        .store_uint(cell_hash(stateinit), 256)
    .end_cell().begin_parse();
}

() claim (slice entry) impure {
    cell stateinit = helper_stateinit(entry);
    send_raw_message(begin_cell()
        .store_uint(0x18, 6)
        .store_slice(helper_address(stateinit))
        .store_coins(0)
        .store_uint(6, 108)
        .store_ref(stateinit)
        .store_uint(context::query_id, 64)
    .end_cell(), 64);
}

() send_tokens (slice recipient, int amount) impure {
    send_raw_message(begin_cell()
        .store_uint(0x18, 6)
        .store_slice(data::jetton_wallet)
        .store_coins(0)
        .store_uint(1, 107)
        .store_ref(begin_cell()
            .store_uint(op::transfer, 32)
            .store_uint(context::query_id, 64)
            .store_coins(amount)
            .store_slice(recipient)
            .store_slice(my_address())
            .store_uint(0, 1)
            .store_coins(1000000)
            .store_uint(0, 1)
        .end_cell())
    .end_cell(), 64);
}

() recv_internal (int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_bits() < 96) {
        return ();
    }

    context::op = in_msg_body~load_uint(32);
    context::query_id = in_msg_body~load_uint(64);
    (_, context::sender) = in_msg_full.begin_parse().skip_bits(4).load_msg_addr();

    load_data();

    if (context::op == op::claim) {
        throw_unless(error::not_enough_coins, msg_value >= 150000000);
        
        int index = in_msg_body~load_uint(256);
        cell proof_cell = in_msg_body~load_ref();

        (slice cs, int exotic?) = proof_cell.begin_parse_exotic();
        throw_unless(42, exotic?);
        throw_unless(43, cs~load_uint(8) == 3);
        throw_unless(44, data::merkle_root == cs~load_uint(256));

        cell dict = cs~load_ref();
        (slice data, int found?) = dict.udict_get?(256, index);
        throw_unless(45, found?);

        claim(data);
    }

    elseif (context::op == op::process_claim) {
        slice entry = in_msg_body;
        slice claimer = in_msg_body~load_msg_addr();
        int amount = in_msg_body~load_coins();

        throw_unless(error::wrong_sender, equal_slices(context::sender, helper_address(helper_stateinit(entry))));

        send_tokens(claimer, amount);
    }

    else {
        throw(0xffff);
    }
}

(slice, int, cell) get_contract_data() method_id {
    load_data();
    return (data::jetton_wallet, data::merkle_root, data::helper_code);
}
